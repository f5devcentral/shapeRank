Newspeak3
'Root'
class HyperStreams usingPlatform: p <Platform> = (
(*
A module for implementing hyperstreams. The implementation is kept as simple (naive) as possible.

A hyperstream is  either a scalar, or a stream whose elements are hyperstreams. A hyperstream has zero or more dimensions. The number of dimensions of a hyperstream is known as its rank. All elements of a hyperstream must have the same rank. Hyperstreams are represented by subclasses of HyperStream.

A scalar is itself a hyperstream,  known as a scalar hyperstream, which is represented by class ScalarHyperStream.  A scalar hyperstream has zero dimensions, and therefore has rank 0; otherwise, a hyperstream has 1 + k dimensions, where k is the rank of of its elements. Each dimension has a size, which may be known or unknown. If the size is unknown, it indicated by ?, which is represented by an instance of class UnboundedDimension; otherwise the size is a positive integer.

*)
  |
    (* imports *)
    private List = p collections List.    
  |
) (
public class HyperStream rank: r <Integer> = (
(* The abstract class at the root of the hyperstream hierarchy.  A hyperstream contains elements of some type T. *)
  |
    window <List[HyperStream[T]]>
    public rank <Integer> = r.
    public dimensions <List[Integer | UnboundedDimension]>
  |
) (
public isKindOfHyperStream ^ <Boolean> = (
  ^true
)
public isComplete: b <Boolean> = (
  subclassResponsibility
)
public isBounded ^ <Boolean> = (
  subclassResponsibility
)
public isComplete  ^ <Boolean> = (
  subclassResponsibility
)
public clone = (
  subclassResponsibility
)
public  asStream ^ <HyperStream[T]> = (
  ^self
)
public map: f <[:T | R]>  ^ <HyperStream[R]> = (
  subclassResponsibility
)
public filter: f <[:T | Boolean]> ^ <HyperStream[T]> = (
  subclassResponsibility
)
public take: n <Integer> ^ <HyperStream[T]> = (
  subclassResponsibility
)
public iterator ^ <Iterator[T | HyperStream[T]]> = (
  suclassResponsibility
)
public isScalar ^ <Boolean> = (
  subclassResponsibility
)
public collect: f <[:T | R]>  ^ <HyperStream[R]> = (
 (* A shallow mapping. Distinct for hyperstreams with a nested structure *)
   ^map: f
)
public contains: element <Object> ^ <Boolean> = (
   subclassResponsibility
)
public map: f <[:T | R]> withRangeOfDimensions: ds <List[Integer | UnboundedDimension]>  ^ <HyperStream[R]> = (
  subclassResponsibility
)
) : (
public fromScalar: o <T def> ^ <ScalarHyperStream[T]> = (
  ^ScalarHyperStream scalarValue: o
)
public empty ^ <EmptyHyperStream> = ( 
  ^EmptyHyperStream new
)
public repeat:  k <T def> dimension: ds <List[Integer | UnboundedDimension]> ^ <ConstantStream[T]> = (
  ^ConstantStream on:  k dimension: ds
)
public fromList: l <List[HyperStream[T def]]> dimensions: ds <List[Integer | UnboundedDimension]> ^ <BoundedHyperSteam[T]> = (
  ^BoundedHyperStream fromList: l dimensions: ds
)
public  iteratingOver: i <Iterator[HyperStream[T def]]>  dimensions: ds <List[Integer | UnboundedDimension]>  ^ <UnboundedHyperStream[T]> = (
   ^UnboundedHyperStream iteratingOver: i dimensions: ds
)
public zipSynchronous: streams <List[HyperStream]> ^ <HyperStream[List]> = (
  ^iteratingOver: (SynchronousZipIterator iterators: (streams collect: [:s <HyperStream> | s iterator])) dimensions: streams first  dimensions
)
public zipReactive: streams <List[HyperStream]> ^ <HyperStream[List]> = (
 | dimTail <List[Integer | UnboundedDimension]> = streams first dimensions copyFrom: 2 to: streams first dimensions size. |

  ^iteratingOver: (ReactiveZipIterator zipping: (streams collect: [:s <HyperStream> | s iterator])) dimensions: {UnboundedDimension new}, dimTail
)
public unboundedDimension = (
  ^UnboundedDimension new
)
public repeat:  k <T def> ^ <ConstantStream[T]> = (
  ^repeat:  k dimension: {UnboundedDimension new}
)
public extensibleWithDimensions: dims <List[Integer | UnboundedDimension]>  ^ <UnboundedHyperStream> = (
  ^ iteratingOver: (ExtensibleIterator rank: dims size) dimensions: dims
)
public extensible ^ <UnboundedHyperStream> = (
  | dims <List[Integer | UnboundedDimension]> = List new. |
  dims add: UnboundedDimension new.
  ^extensibleWithDimensions: dims
)
)
class ConstantStream on: k <T def> dimension: ds <List[Integer | UnboundedDimension]> = HyperStream rank: ds size (
(* A hyperstream , all of whose elements are equal to some fixed elemnt  *)
  |
  constant <T> = k.
  |
  dimensions:: ds.
) (
class Iterator = outer HyperStreams Iterator (
) (
public hasNext ^ <Boolean> = (
  ^true
)
public next ^ <T> = (
  ^constant
)
public rank ^ <Integer> = (
  ^0
)
) : (
)
public isComplete  ^ <Boolean> = (
  ^false (* Is this the right answer?*)
)
public isComplete: b  <Boolean> = (
   (* Do nothing? *)
)
public clone = (
)
public filter: f <[:T | Boolean]> ^ <HyperStream[T]> = (
  (f value: constant) ifTrue: [^self] ifFalse: [^EmptyStream].
)
public map: f <[:T | R]>  ^ <HyperStream[R]> = (
  ^class on: (f value: constant)
)
public iterator ^ <Iterator[T | HyperStream[T]]> = (
  ^Iterator new
)
public isScalar ^ <Boolean> = (
  ^false (* Is this the right answer?*)
)
public contains: element <Object> ^ <Boolean> = (
  ^element = constant
)
public take: n <Integer> ^ <HyperStream[T]> = (
  | l <List> = List new: n. |
  1 to: n do: [:i <Integer> | l add: constant].
  ^BoundedStream fromList: l dimensions: {n}
)
public hash ^ <Integer> = (
  ^constant hash bitXor: class hash
)
public = other <Object> ^ <Boolean>  = (
  ^constant = other constant
)
public printString ^ <String> = (
  ^'[', constant printString, ' ...]'
)
public map: f <[:T | R]> withRangeOfDimensions: ds <List[Integer | UnboundedDimension]>  ^ <HyperStream[R]> = (
  ^class on: (f value: constant) dimensions: ds
)
) : (
)
public class BoundedHyperStream fromList: l <List[HyperStream[T]]> dimensions: ds <List[Integer | UnboundedDimension]> = HyperStream rank: ds size (
(* A hyperstream whose first dimension is a known integer.  The rank of a bounded hyperstream is always at least 1; rank 0 cases are represented by ScalarHyperStream.
Hence, the window and dimensions are never empty, and the window elements elements are always hyperstreams, bottoming out as scalar hyperstreams.
*)
| public iterator <Iterator[HyperStream[T]]> = Iterator new. |
  window: l.
  dimensions: ds.
) (
class Iterator = outer HyperStreams Iterator (
  |
  index <Integer> ::= 0.
  |
) (
public hasNext ^ <Boolean> = (
  ^(index = window size) not.
)
public rank ^ <Integer> = (
  ^outer BoundedHyperStream rank
)
public clone = (
  ^class new
)
public next ^ <HyperStream[T]> = (
  hasNext ifTrue: [
    index:: index + 1.
    ^window at: index
    ].
  Error signal: 'Attempt to access unavailable stream element' 
)
) : (
)
public isBounded ^ <Boolean> = (
  ^true
)
public isKindOfBoundedHyperStream ^ <Boolean> = (
  ^true
)
public isComplete  ^ <Boolean> = (
  ^true
)
public isComplete: b <Boolean> = (
  (*  do nothing, but check that no one tries to tell me I'm not complete *)
  assert: [b] message: 'Attempt to assert bunded stream is incomplete!'
)
public clone  = (
  ^class fromList: window dimensions: dimensions
)
public size ^ <Integer> = (
  ^window size
)
window = (
  (* Make available in lexical scope, so Iterator can access *)
  ^super window
)
public isScalar ^ <Boolean> = (
  ^false
)
public isKindOfTensor ^ <Boolean> = (
  (* Needed for compatibility with old tests *)
  ^true 
)
public contents ^ <List[HyperStream[T]]> = (
  (* For temporary compatibility with old tests *)
  #BOGUS. 
  ^window
)
public take: n <Integer> ^ <BoundedHyperStream[T]> = (
| 
dimHead <List[Integer | UnboundedDimension]> = List withAll: {n}.
dimTail <List[Integer | UnboundedDimension]> = dimensions copyFrom: 2 to: rank. 
|
  (* Need to protect against taking more than the window size *)
  assert: [n <= window size] message: 'Attempt to access a bounded stream past its end'.
  ^class fromList: (window copyFrom: 1 to: n) dimensions: dimHead,  dimTail
)
public collect: f <[:T | R]>  ^ <HyperStream[R]> = (
 (* A shallow mapping. Distinct for hyperstreams with a nested structure *)
 | 
 collected <List[HyperStream[R]]> = window collect: f.
 dimHead <List[Integer | UnboundedDimension]> = List withAll: {dimensions first}.
 dimTail <List[Integer | UnboundedDimension]> = 
   collected isEmpty ifFalse: [collected first dimensions] ifTrue: [List new].
 |
  ^class fromList: collected dimensions: dimHead,  dimTail
)
public filter: f <[:T | Boolean]> ^ <BoundedHyperStream[T]> = (
  | dtail = dimensions copy from: 2 to: dimensions size. |
  (* dimension computation is dubious at best *)
  ^class fromList: (window select: [:e <HyperStream[T]> | e filter: f]) dimensions: {UnboundedDimension new}, dtail 
)
public = other <Object> ^ <Boolean> = (
  other isKindOfBoundedHyperStream ifFalse: [^false].
  other dimensions  with:  dimensions do: [:od :d | od = d ifFalse: [^false]].
  other contents with: contents do: [:oe :e | oe = e ifFalse: [^false]].
  ^true.
)
public hash ^ <Integer> = (
  ^dimensions hash bitXor: contents hash.
)
public map: f <[:T | R]>  ^ <HyperStream[R]> = (
  | 
  newWindow <List[HyperStream[R]]> = window collect:  [:e <HyperStream[T]> | e map: f]. 
  dimStart <Integer> = List withAll: {dimensions first}.
  |
  ^class fromList: newWindow dimensions: dimStart, newWindow first dimensions
)
public contains: element <Object> ^ <Boolean> = (
   window detect: [:hs <HyperStream[T]> | hs contains: element] ifNone: [^false].
   ^true
)
public printString ^ <String> = (
  | contentString <String> |
      contentString:: ''.
	   window do: [:e | contentString:: contentString, e printString] 
	        separatedBy: [contentString:: contentString, ', '].
	   ^'[', contentString, ']'.
)
public map: f <[:T | R]> withRangeOfDimensions: ds <List[Integer | UnboundedDimension]>  ^ <HyperStream[R]> = (
(* should we assert that result of map has the dimensions expected? *)
  ^map: f
)
public zip ^ <Hyperstream[T]> = (
  | ds = dimensions copyFrom: 1 to: dimensions size. |
  ds size > 1 ifTrue: [ds at:1 put: (dimensions at:2). ds at: 2 put: dimensions first].
(* We really should support transpose, but this is enough for many cases *)
#BOGUS.
  ^HyperStream iteratingOver: (ReactiveZipIterator zipping: (window collect: [:s <HyperStream> | s iterator])) dimensions: ds
)
) : (
)
public class UnboundedHyperStream iteratingOver: i <Iterator[HyperStream[T def]]> dimensions: ds <List[Integer | UnboundedDimension]>  = HyperStream rank: ds size (
  |
  public iterator <Iterator[HyperStream[T]]> = i.
  public isComplete <Boolean> ::= false.
  |
  window:: List new.
  dimensions:: ds.
  assert: i rank = ds size message: 'Iterator rank and stream rank disagree'.
) (
public isKindOfUnboundedHyperStream ^ <Boolean> = (
  ^true
)
public isBounded ^ <Boolean> = (
  ^false
)
public map: f <[:T | R]>  ^ <HyperStream[R]> = (
  ^class iteratingOver: (iterator map:  [:e <HyperStream[T]> | e map: f]) dimensions: dimensions 
)
public filter: f <[:T | Boolean]> ^ <BoundedHyperStream[T]> = (
  ^class iteratingOver: (iterator map: [:e <HyperStream[T]> | e filter: f]) dimensions: dimensions 
)
public isScalar ^ <Boolean> = (
  ^false
)
public = other <Object> ^ <Boolean> = (
  other isKindOfUnboundedHyperStream ifFalse: [^false].
  other dimensions with:  dimensions do: [:od :d | od = d ifFalse: [^false]].
  ^iterator = other iterator
)
public hash ^ <Integer> = (
  ^iterator hash bitXor: class hash
)
public clone = (
  ^class iteratingOver: iterator clone dimensions: dimensions
)
public take: n <Integer> ^ <HyperStream[T]> = (
  | dimTail = dimensions copyFrom: 2 to: dimensions size. |
  ^class iteratingOver: (iterator take: n)  dimensions:  {n}, dimTail
)
public next ^ <HyperStream[T]> = (
  | n <HyperStream[T]> = iterator next. |
  window add: n.
  ^n
)
public contains: element <Object> ^ <Boolean> = (
  (* Semantics of contains on hyperstreams are non-deterministic. We may never return a result, but if we do, it is always the same.
       This implementation is depth first, but others may differ
  *)
   | it = iterator clone. |
   [true] whileTrue: [
      it hasNext 
        ifTrue: [(it next contains: element) ifTrue: [^true]]
        ifFalse: [isComplete ifTrue: [^false]]
   ]
)
public printString ^ <String> = (
  | contentString <String> |
      contentString:: ''.
      window do: [:e | contentString:: contentString, e printString] 
	        separatedBy: [contentString:: contentString, ', '].
	   ^'[', contentString, ', ...]'.
)
public collect: f <[:T | R]>  ^ <HyperStream[R]> = (
 (* A shallow mapping. Distinct for hyperstreams with a nested structure *)
  ^class iteratingOver: (iterator map: f withRank: rank) dimensions: dimensions 

)
public map: f <[:T | R]> withRangeOfDimensions: ds <List[Integer | UnboundedDimension]>  ^ <HyperStream[R]> = (
  ^class iteratingOver: (iterator map:  [:e <HyperStream[T]> | e map: f withRangeOfDimensions: ds] withRank: ds size) dimensions: ds 
)
public hasNext ^ <Boolean> = (
  ^iterator hasNext
)
) : (
)
class MappedIterator mapping: f <[:T | R]> on: i <Iterator> rank: r <Integer> =  DerivedIterator fromIterator: i (
  |
  function <[:T | R]> = f.
  public rank <Integer> = r.
  |
) (
public next ^ <R> = (
  ^function value: iterator next
)
public clone = (
  ^class mapping: function on: iterator clone rank: rank
)
) : (
)
class DerivedIterator fromIterator: i <Iterator[T def]> =  Iterator (
  |
  iterator <Iterator[T]> = i.
  |
) (
public hasNext ^ <Boolean> = (
  ^iterator hasNext
)
public rank ^ <Integer> = (
  ^iterator rank
)
) : (
)
public class Iterator = (
  (* Abstract class at root of the iterator hierarchy for hyperstreams.  An iterator is a one dimensional sequence of elements of a type T.  In this module, they serve to represent a stream of hyperstreams that underlies a[n unbounded] hyperstream, and so T is usually HyperStream[S] for some S.*)
) (
public hasNext ^ <Boolean> = (
  ^subclassResponsibility
)
public next ^ <T> = (
  ^subclassResponsibility
)
public filter: f <[:T | Boolean]>  = (
  ^FilteredIterator filtering: f on: self
)
public rank ^ <Integer> = (
  ^subclassResponsibility
)
public clone = (
  self subclassResponsibility
)
public map: f <[:T | R]> withRank: r <Integer> = (
  ^MappedIterator mapping: f on: self rank: r 
)
public map: f <[:T | R]> = (
  ^MappedIterator mapping: f on: self rank: rank
)
) : (
)
class FilteredIterator mapping: f <[:T | Boolean]> on: i <Iterator> =  DerivedIterator fromIterator: i (
  |
  function <[:T | Boolean]> = f.
  peek <T | Nil>
  |
) (
lookahead = (
  | nextItem <T | Nil> nextIsValid <Boolean>  |
  [iterator hasNext ifTrue: [nextIsValid:: (function value: (nextItem: iterator next))] ifFalse: [true]] whileFalse: [].
  ^nextIsValid ifTrue: [nextItem] ifFalse: [nil].
)
public hasNext ^ <boolean>  = (
   (* We're working on a stream, so even if we had nothing before, we might have something now. *)
   ^peek isNil not or: [peek:: lookahead. peek isNil not]
)
public next ^ <T> = (
(* Iffy. Only works if we assume someone has called hasNext just before calling next. 
   We should probably call hasNext here.  
*)
 | result = peek. |
 peek isNil ifTrue: [Error signal].
 peek:: lookahead.
  ^result
)
public clone = (
  ^class mapping: function on: iterator clone
)
) : (
)
class ReactiveZipIterator zipping: iters <List[Iterator]> = Iterator (
(*
Iterator for reactive function calls. Takes a list of iterators on the arguments, and produces an iterator for their zipped values. If any argument has a value ready, produce a list of zipped values. Iterators that do not have a next value re-use their previous values, which are kept in the lastValues slot. Of course, this can't work the first time  the iterator is called, since there are no \"previous values\".  This is a special case, tracked by the firstTime slot.
*)
  |
  iterators <List[Iterator]> = iters.
  firstTime <Boolean> ::= true.
  lastValues <List> = List new: iterators size.
  |
) (
first ^ <List> = (
(* Produce  the first value of the iterator.  This is a special case, as noted in the class comment.*)
  iterators do: [:iter <Iterator> |
     (* initially populate lastValues *)  
     lastValues add: (iter hasNext 
       ifTrue: [iter next]
       (* We require that hasNext was call before calling this method, which will ensure that all iter hasNext is true for all iterators. Hence, if it is not the case, we have an error.*)
       ifFalse: [Error signal: 'Called iterator next without verifying if hasNext is true']
    )
  ].
  (* Ok, we are past the first time now *)
  firstTime:: false.
  (* Our results are in lastValues, but we do not want to expose that, so we copy *)
  ^lastValues copyFrom: 1 to: iterators size
)
public hasNext ^ <Boolean> = (
  (* First call is a special case, per class comment. We need initial values for all iterators. *)
  firstTime ifTrue: [^iterators allSatisfy: [:iter <Iterator> | iter hasNext]].
  (* normal case - as long as any iterator has a new value, we are ok. *)
  ^iterators anySatisfy: [:iter <Iterator[T]> | iter hasNext].
)
public next ^ <List> = (
  | result <List> = List new: iterators size. |
  firstTime ifTrue: [^first].
  assert: [hasNext] message: 'Called iterator next without verifying if hasNext is true'.
  (* We require that hasNext was call before calling this method. If it is not the case, we have an error.*)  
  iterators with: lastValues do: [:iter <Iterator> :last <Object> |
     result add: (iter hasNext 
       ifTrue: [iter next]
       ifFalse: [last]
    )
  ].
  ^result
)
public clone = (
  ^class zipping: (iterators collect: [:i <iterator> | i clone])
)
public rank ^ <Integer> = (
  ^iterators first rank + 1
)
) : (
)
class SynchronousZipIterator zipping: iters <List[Iterator]> = Iterator (
  |
  iterators <List[Iterator]> = iters.
  |
) (
public hasNext ^ <Boolean> = (
  ^iterators allSatisfy: [:iter <Iterator[T]> | iter hasNext].
)
public next ^ <List[T]> = (
  ^iterators collect: [:iter <Iterator[T]> | iter next ]
)
public rank ^ <Integer> = (
  ^iterators first rank
)
public clone = (
  ^class zipping: (iterators collect: [:i <iterator> | i clone])
)
) : (
)
class ReplicatingIterator replicating: s <HyperStream[T def]> by: i <Iterator> = DerivedIterator fromIterator: i (
(* Replicate stream s once per element of i *)
  |
  replicated <HyperStream[T]> = s.
  |
) (
public rank ^ <Integer> = (
  ^iterator rank + 1
)
public next ^ <HyperStream[T]> = (
  iterator next.
  ^replicated
)
public clone = (
  ^class replicating: replicated clone by: iterator clone
)
) : (
)
public class UnboundedDimension = (
  (* The magic object representing an unbounded dimension of a hyperstream *)
) (
public = other ^ <Boolean> = (
  ^other isKindOfUnboundedDimension
)
public hash ^ <Integer> = (
  ^class hash
)
public isKindOfUnboundedDimension ^ <Boolean> = (
  ^true
)
public printString ^ <String> = (
  ^'?'
)
) : (
)
class ScalarHyperStream scalarValue: s <T def> =  HyperStream rank: 0 (
  |
  public constant <T> = s.
  public iterator <Iterator[T]> = Iterator new.
  |
    window:: List withAll: {s}.
    dimensions:: List new.
) (
class Iterator = outer HyperStreams Iterator (
  | accessed <Boolean> ::= false. |
) (
public next ^ <T> = (
  ^accessed ifFalse: [accessed:: true. constant] ifTrue: [Error signal: 'scalar iterator exhausted']
)
public clone = (
  ^class new
)
public rank ^ <Integer> = (
  ^0
)
public hasNext ^ <Boolean> = (
  ^accessed not
)
) : (
)
public filter: f <[:T | Boolean]> ^ <Stream[T]> = (
  ^(f value: constant) ifTrue: [self] ifFalse: [HyperStream empty]
)
public isBounded ^ <Boolean> = (
  ^true
)
public isComplete ^ <Boolean> = (
  ^true
)
public isComplete: b <Boolean> = (
  (*  do nothing, but check that no one tries to tell me I'm not complete *)
  assert: [b] message: 'Attempt to assert bunded stream is incomplete!'
)
public isKindOfScalarHyperStream ^ <Boolean> = (
  ^true
)
public isScalar ^ <Boolean> = (
  ^true
)
public take: n <Integer> ^ <Stream[T]> = (
  assert: [n < 2] message: 'Attempt to access more than one element of a scalr stream'.
  ^self
)
public clone = (
 ^class scalarValue: constant
)
public asScalar ^ <Object> = (
  ^constant
)
public isKindOfTensor ^ <Boolean> = (
  (* Needed for compatibility with old tests *)
  ^true 
)
public map: f <[:T | R]>  ^ <HyperStream> = (
  | result <R> = f value: constant. | 
  result isKindOfHyperStream ifTrue: [^result].
  ^class scalarValue: result
)
public contents ^ <List[HyperStream[T]]> = (
  (* For temporary compatibility with old tests *)
  #BOGUS. 
  ^List withAll: {constant}
)
public = other <Object> ^ <Boolean> = (
  other isScalar ifTrue: [^constant = other constant].
  ^false.
)
public hash ^ <Integer> = (
  ^constant hash bitXor: class hash
)
public contains: element <Object> ^ <Boolean> = (
   element isKindOfScalarHyperStream ifTrue: [^constant = element asScalar].
   ^constant = element
)
public printString ^ <String> = (
  ^constant printString
)
public map: f <[:T | R]> withRangeOfDimensions: ds <List[Integer | UnboundedDimension]>  ^ <HyperStream[R]> = (
  ^map: f
)
) : (
)
class EmptyHyperStream = HyperStream rank: 0(

) (
public isBounded ^ <Boolean> = (
  ^true
)
public isComplete ^ <Boolean> = (
  ^true
)
public isComplete: b <Boolean> = (
  (*  do nothing, but check that no one tries to tell me I'm not complete *)
  assert: [b] message: 'Attempt to assert bunded stream is incomplete!'
)
public isKindOfEmptyHyperStream ^ <Boolean> = (
  ^true
)
public add: e <T> = (
  Error signal: 'Cannot add elements to an empty stream'
)
public filter: f <[:T | Boolean]> ^ <Stream[T]> = (
)
public take: n <Integer> ^ <Stream[T]> = (
  assert: [n = 0] message: 'Attempt to element(s) of an empty stream'.
  ^self
)
public map: f <[:T | R]>  ^ <EmptyHyperStream[R]> = (
)
public clone = (
)
public iterator ^ <Iterator[T | HyperStream[T]]> = (
  ^EmptyIterator new
)
public isScalar ^ <Boolean> = (
  ^false (* Is this the right answer?*)
)
public isKindOfTensor ^ <Boolean> = (
  (* Needed for compatibility with old tests *)
  ^true 
)
public contains: element <Object> ^ <Boolean> = (
   ^false
)
public printString ^ <String> = (
  ^''
)
public map: f <[:T | R]> withRangeOfDimensions: ds <List[Integer | UnboundedDimension]>  ^ <HyperStream[R]> = (
)
) : (
)
class EmptyIterator = Iterator (
) (
public hasNext ^ <Boolean> = (
  ^false
)
public rank ^ <Integer> = (
  ^0
)
public clone = (
)
) : (
)
class ExtensibleIterator rank: r <Integer> = Iterator (
	|
	contents <List[T]> = List new.
    index <Integer> ::= 0.
    public rank <Integer> = r.
	|
) (
public hasNext ^ <Boolean> = (
  ^index < contents size
)
public next ^ <T> = (
  index:: index + 1.
  ^contents at: index
)
public add: e <T> = (
 contents add: e
)
public clone ^ <Self> = (
  | result <Self> = class new. |
  contents do: [:e | result add: e].
  ^result
)
) : (
)
) : (
)
